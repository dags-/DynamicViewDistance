buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

plugins {
    id "net.minecrell.vanillagradle.server" version "2.2-3"
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

apply plugin: "org.spongepowered.mixin"

group = "me.dags"
version = "0.2.0-SNAPSHOT"
def spongeAPI = "7.1.0"
def spongeChannel = "SNAPSHOT"
sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.12.2"
    mappings = "snapshot_20170919"
    makeObfSourceJar = false
}

sourceSets {
    main { refMap = "mixins.dynview.refmap.json" }
}

repositories {
    maven { url = "https://repo.spongepowered.org/maven" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeAPI}-${spongeChannel}"
    compileOnly "org.spongepowered:mixin:0.6.6-SNAPSHOT"
    compile "com.github.dags-:cfg:0.5.1-SNAPSHOT"
    compile "com.github.dags-:CommandBus:2.0.6"
}

shadowJar {
    exclude "dummyThing"
    relocate "me.dags.commandbus.", "me.dags.dynview.commandbus."
    relocate "me.dags.config.", "me.dags.dynview.cfg."
    classifier null
}

jar {
    manifest.attributes(
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "MixinConfigs": "mixins.dynview.json",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "TweakOrder": 0
    )
}

processResources {
    from("build/tmp/compileJava") {
        include "*.refmap.json" // make sure refmap (generated during compileJava) is included as a resource
    }
}

reobfJar {
    // replace the jar task .jar with the shadowJar task .jar before reobf"ing (there"s probably a better way)
    dependsOn shadowJar
}